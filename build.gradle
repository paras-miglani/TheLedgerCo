plugins {
    id 'java'
    id 'jacoco'
    id "io.freefair.lombok" version "2.9.5"
}

group 'com.geektrust.theledgerco'
version '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    archiveBaseName = 'geektrust' //Please do not change this final artifact name
    version = null               //Please do not change this final artifact version
    manifest {
        attributes 'Main-Class' : 'com.geektrust.theledgerco.App' //This is main class of your program which will be
        // executed
    }

    // To create a single jar with all dependencies.
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

test { ///Please do not change this
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_ERROR"
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
    annotationProcessor 'org.projectlombok:lombok'
}

jacoco {
    toolVersion = "0.8.4"
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
        xml.destination file("./jacoco.xml")
    }
}